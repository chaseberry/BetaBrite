def $versionNumber = '0.0.1'//Current uploaded version

group 'edu.csh.chase.kbetabrite'
version $versionNumber

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

apply plugin: 'kotlin'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

repositories {
    mavenCentral()
    jcenter()
}

buildscript {
    ext.kotlin_version = '0.14.449'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
    test.java.srcDirs += 'src/test/kotlin'
}

jar {
    from(configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }

    manifest {
        attributes 'Main-Class': "edu.csh.chase.kbetabrite.Main"
    }
}

bintray {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    user = properties.getProperty('bintray.user')
    key = properties.getProperty('bintray.apiKey')
    publications = ['betabritePublication']
    pkg {
        repo = 'maven'
        name = 'KBetaBrite'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/chaseberry/KBetaBrite.git'
        version {
            name = $versionNumber
            desc = 'A library for interacting with BetaBrite signs'
            released = new Date()
        }
    }
}

publishing {
    publications {
        betabritePublication(MavenPublication) {
            from components.java
            groupId 'edu.csh.chase.kbetabrite'
            artifactId 'betabrite'
            version $versionNumber
        }
    }
}